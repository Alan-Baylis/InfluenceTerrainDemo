#pragma kernel CSMain

Texture2D<float4> mask;
int border;
float turbulence = 0.1;
Texture2D<float4> noise;
Texture2D<float4> grass;
Texture2D<float4> stone;
Texture2D<float4> water;
Texture2D<float4> sand;
Texture2D<float4> snow;
RWTexture2D<float4> result;				//Result baked texture

SamplerState _LinearClamp;
SamplerState _LinearRepeat;

float2 scaleUV(float2 uv, float borderF)
{
    float2 result = float2(uv.x * (1 - borderF * 2) + borderF, uv.y * (1 - borderF * 2) + borderF);		//todo consider use FMA function
	return result; 
}

[numthreads(8,8,1)]
void CSMain (uint2 id : SV_DispatchThreadID)
{
	float w, h;
    result.GetDimensions(w, h);
	float2 uv = float2(id.x/(w - 1), id.y/(h - 1));

	//Calculate turbulence for mask
	float4 turbulenceValue = (noise.SampleLevel(_LinearRepeat, uv, 0) - 0.5) * turbulence;

	//Compensate mask border
	float maskW, maskH;
	mask.GetDimensions(maskW, maskH);
	float borderF = border/maskW;

	uv = scaleUV(uv, borderF);

	//Apply turbulence to mask
	uv = uv + float2(turbulenceValue.x, turbulenceValue.y);

	//Get mask value for texel
	float4 masked = mask.SampleLevel(_LinearClamp, uv, 0);

	//Lerp textures
	float4 resultPixel = sand[id];
	resultPixel = lerp(resultPixel, stone[id], masked.r);
	resultPixel = lerp(resultPixel, grass[id], masked.g);
	resultPixel = lerp(resultPixel, water[id], masked.b);
	resultPixel = lerp(resultPixel, snow[id], masked.a);

    result[id] = resultPixel;
}
