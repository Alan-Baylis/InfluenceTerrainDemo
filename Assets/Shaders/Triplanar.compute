// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

Texture2D<float4> FlatTexture;			//Large source chunk texture
Texture2D<float4> SteepTexture;			//Large source chunk texture
Texture2D<float3> HeightMap;			//Small vertex height map
Texture2D<float3> Normals;				//Small block normal map
int Border;								//Normal map border (for proper cross-chunk blending)
float Lower;							//World meters height for 0 in HeightMap
float Upper;							//World meters height for 1 in HeightMap
float SteepAngleFrom;					//From-To angles to blend between Flat and Steep textures
float SteepAngleTo;
//int2 WorldPos;

RWTexture2D<float4> Result;			//Large result chunk texture

SamplerState _LinearClamp;
SamplerState _PointClamp;

#include "Assets\Shaders\Common.cginc"

[numthreads(8,8,1)]
void CSMain (uint2 id : SV_DispatchThreadID)
{
	//Based on http://gamedevelopment.tutsplus.com/articles/use-tri-planar-texture-mapping-for-better-terrain--gamedev-13821

	float2 dim;	
    Result.GetDimensions(dim.x, dim.y);
	float2 uv = float2(id.x/(dim.x - 1), id.y/(dim.y - 1));
	float texSize = dim.x / 16;

	//Get texel world space
	float heightMapSize;
	HeightMap.GetDimensions(heightMapSize, heightMapSize);
	float2 heightUV = scaleUV(uv, 0.5, heightMapSize);
	float3 wTexel = HeightMap.SampleLevel(_LinearClamp, heightUV, 0);			//Set normalized height as y
	wTexel.x = id.x;
	wTexel.z = id.y;
	wTexel.y = (lerp(Lower, Upper, wTexel.y));
	
	//Scale uv because of border
	float normalsSize;
	Normals.GetDimensions(normalsSize, normalsSize);
	float2 normalUV = scaleUV(uv, Border, normalsSize);
	
	//Get blending weights
	float3 wNorm = Normals.SampleLevel(_LinearClamp, normalUV, 0);
	wNorm = wNorm * 2 - 1;							//Restore normal vector from color
	float3 blending = abs( wNorm );
	blending = normalize(max(blending, 0.00001)); 

	//Get blending for slope textures
	wTexel.y = (wTexel.y * texSize) % dim.x;				//Convert meters to texels
	float2 slopeAxis = float2(blending.x, blending.z);
	float slopeSum = blending.x + blending.z;
	slopeAxis /= float2(slopeSum, slopeSum);
	float4 steepPixel = SoftDepthBlend(SteepTexture[wTexel.zy], SteepTexture[wTexel.xy], slopeAxis.y);			//Check members

	//Blend with flat texture
	float angle = degrees(acos(wNorm.y));			//Get fragment world angle to vertical
	Result[id] = SoftDepthBlend(FlatTexture[wTexel.xz], steepPixel, Compress(angle, SteepAngleFrom, SteepAngleTo));
}
